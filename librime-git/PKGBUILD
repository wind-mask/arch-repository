# Maintainer: wind_mask <YOU KNOW WHO>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: GONG Chen <chen dot sst at gmail dot com>
# Contributor: 網軍總司令

_pkgname=librime
pkgname=librime-git
pkgver=1.14.0.r11.g03d944c
pkgrel=1
epoch=1
pkgdesc="Rime input method engine"
arch=('x86_64')
url="https://github.com/rime/librime"
license=('BSD-3-Clause')
depends=('boost-libs' 'capnproto' 'gcc-libs' 'glibc' 'opencc' 'yaml-cpp' 'leveldb' 'librime-data' 'lua' 'google-glog' 'marisa')
makedepends=('git' 'cmake' 'boost' 'gtest' 'ninja')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git+https://github.com/rime/librime.git"
        "git+https://github.com/lotem/librime-octagram.git"
        "git+https://github.com/hchunhui/librime-lua.git"
        "git+https://github.com/rime/librime-charcode.git"
        "git+https://github.com/lotem/librime-proto.git"
        "git+https://github.com/rime/librime-predict.git")
sha512sums=('SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP')

pkgver() {
  cd "$_pkgname"
  git describe --long --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}
prepare() {
  cd librime/plugins
  ln -sf "$srcdir"/librime-octagram
  ln -sf "$srcdir"/librime-lua
  ln -sf "$srcdir"/librime-charcode
  ln -sf "$srcdir"/librime-proto
  ln -sf "$srcdir"/librime-predict
}

build() {
  cd "$_pkgname"
  make merged-plugins
}

check() {
  cd "$_pkgname/build"
}

package() {
  cd "$_pkgname"
  mkdir -p "${pkgdir}/usr/share/licenses/${_pkgname}"
  cp LICENSE "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
  cd build
  make install DESTDIR="${pkgdir}" prefix=/usr
}
